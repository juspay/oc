{
  description = "One-click coding agents";

  nixConfig = {
    extra-substituters = "https://cache.nixos.asia/oss";
    extra-trusted-public-keys = "oss:KO872wNJkCDgmGN3xy9dT89WAhvv13EiKncTtHDItVU=";
  };

  inputs = {
    flake-parts.url = "github:hercules-ci/flake-parts";
    llm-agents.url = "github:numtide/llm-agents.nix";
    nixpkgs.follows = "llm-agents/nixpkgs";
    nix-agent-wire.url = "github:srid/nix-agent-wire";
  };

  outputs = inputs@{ self, flake-parts, llm-agents, nixpkgs, nix-agent-wire, ... }:
    flake-parts.lib.mkFlake { inherit inputs; } {
      systems = [ "x86_64-linux" "aarch64-linux" "aarch64-darwin" ];

      perSystem = { self', lib, pkgs, system, ... }:
        let
          opencode = pkgs.llm-agents.opencode;
          # callPackageWith auto-injects opencode into package functions that accept it
          callOc = path: lib.callPackageWith (pkgs // { inherit opencode; }) (./coding-agents/opencode/packages + "/${path}");
          juspayConfigFile = callOc "config.nix" { };
          baseConfigFile = callOc "config.nix" { settings = import ./coding-agents/opencode/settings; };
          skillsSrc = self + "/.agents";
        in
        {
          _module.args.pkgs = import nixpkgs {
            inherit system;
            config.allowUnfree = true;
            overlays = [ llm-agents.overlays.default ];
          };

          packages = {
            default = callOc "default.nix" {
              inherit (self'.packages) opencode-juspay-editable opencode-juspay-oneclick opencode-oneclick;
            };
            inherit opencode;
            opencode-juspay-editable = callOc "juspay-editable.nix" {
              configFile = juspayConfigFile;
            };
            opencode-juspay-oneclick = callOc "juspay-oneclick.nix" {
              configFile = juspayConfigFile;
              inherit skillsSrc;
            };
            opencode-oneclick = callOc "oneclick.nix" {
              configFile = baseConfigFile;
              inherit skillsSrc;
            };
          };

          apps = lib.mapAttrs (_: pkg: { program = lib.getExe pkg; }) self'.packages;
        };

      flake.homeModules =
        let
          baseImports = [
            ./coding-agents/opencode/home
            nix-agent-wire.homeModules.opencode
          ];
        in
        {
          opencode = { imports = baseImports; };
          opencode-juspay = {
            imports = baseImports ++ [
              ./coding-agents/opencode/home/juspay.nix
            ];
          };
        };
    };
}
